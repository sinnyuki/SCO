TITLE:

    Moderator is SCS;
   
DATA:
FILE = "./OUTPUT/SEM/Mplus/SEM.mod.SCS_ee0e1081432d0e4df98864fdafd0c696.dat";
 
VARIABLE:
NAMES = Blue1 Blue2 Blue3 Blue4 Blue5 Blue6 Blue7 DI1 DI2 DI3 DC1 DC2 DC3 UI1 UI2
     UI3 UC1 UC2 UC3 ASC1 ASC2 ASC3 ASC4 ASC5 OSC1 OSC2 OSC3 OSC4 OSC6 OSC7 POS1
     POS2 POS3 POS5 POS6 RD1 RD2 RD3 RD4 RD5 LS1 LS2 LS3 LS4 LS5; 
 MISSING=.;
 
ANALYSIS:

    TYPE =general RANDOM;  
    estimator=BAYES; 
    ALGORITHM=INTEGRATION;
    convergence=0.01;
   
MODEL:
  
    Blue by Blue1-Blue7;
    
    ASC by ASC1-ASC5;
    OSC by OSC1-OSC4 OSC6 OSC7;
    SCO by ASC OSC;

    SCO@1;
    
    UI by UI1-UI3;
    UC by UC1-UC3;
    DI by DI1-DI3;
    DC by DC1-DC3;
   
    SCOUI | SCO XWITH UI;
    SCOUC | SCO XWITH UC;
    SCODI | SCO XWITH DI;
    SCODC | SCO XWITH DC;
    
    RD by RD1-RD5;
    POS by POS1-POS3 POS5 POS6;
    
    LS by LS1-LS5;
    
    RD on Blue
          SCO
          UI(a1i)
          UC(a1c)
          SCOUI(w1i)
          SCOUC(w1c);
    POS on Blue
           SCO
           DI(a2i)
           DC(a2c)
           SCODI(w2i)
           SCODC(w2c);
    LS on Blue
          UI 
          UC
          DI 
          DC
          SCO(c)
          RD(b1)
          POS(b2);
   
MODEL CONSTRAINT:

   NEW(L_SCO M_SCO H_SCO
   
      RD_I RD_C POS_I POS_C
   
      L_RD_I M_RD_I H_RD_I
      L_RD_C M_RD_C H_RD_C
   
      L_POS_I M_POS_I H_POS_I
      L_POS_C M_POS_C H_POS_C

      LM_RD_I LH_RD_I MH_RD_I
      LM_RD_C LH_RD_C MH_RD_C
   
      LM_POS_I LH_POS_I MH_POS_I
      LM_POS_C LH_POS_C MH_POS_C
      );
   
    L_SCO = -1;
    M_SCO = 0;
    H_SCO = 1;
   
    RD_I = a1i*b1;
    RD_C = a1c*b1;
    POS_I = a2i*b2;
    POS_C = a2c*b2;
   
    L_RD_I = (a1i+w1i*L_SCO)*b1;
    M_RD_I = (a1i+w1i*M_SCO)*b1;
    H_RD_I = (a1i+w1i*H_SCO)*b1;
   
    L_RD_C = (a1c+w1c*L_SCO)*b1;
    M_RD_C = (a1c+w1c*M_SCO)*b1;
    H_RD_C = (a1c+w1c*H_SCO)*b1;
   
    L_POS_I = (a2i+w2i*L_SCO)*b2;
    M_POS_I = (a2i+w2i*M_SCO)*b2;
    H_POS_I = (a2i+w2i*H_SCO)*b2;
   
    L_POS_C = (a2c+w2c*L_SCO)*b2;
    M_POS_C = (a2c+w2c*M_SCO)*b2;
    H_POS_C = (a2c+w2c*H_SCO)*b2;
   
    LM_RD_I = L_RD_I - M_RD_I;
    LH_RD_I = L_RD_I - H_RD_I;
    MH_RD_I = M_RD_I - H_RD_I;
   
    LM_RD_C = L_RD_C - M_RD_C;
    LH_RD_C = L_RD_C - H_RD_C;
    MH_RD_C = M_RD_C - H_RD_C;
    
    LM_POS_I = L_POS_I - M_POS_I;
    LH_POS_I = L_POS_I - H_POS_I;
    MH_POS_I = M_POS_I - H_POS_I;
   
    LM_POS_C = L_POS_C - M_POS_C;
    LH_POS_C = L_POS_C - H_POS_C;
    MH_POS_C = M_POS_C - H_POS_C;
   
   
MODEL PRIORS:

    c ~ N(-0.063, 0.023);
   
OUTPUT:

    cint;
    STDYX
