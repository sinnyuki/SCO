TITLE:

    Moderator is USC DSC;
   
DATA:
FILE = "./OUTPUT/Mplus/SEM.mod.USC_DSC_9b00c9913ee444a01c46afb7c6cdf4ea.dat";
 
VARIABLE:
NAMES = ASC1 ASC2 ASC3 ASC4 ASC5 OSC1 OSC2 OSC3 OSC4 OSC5 OSC6 RD1 RD2 RD3 RD4 RD5
     POS1 POS2 POS3 POS4 POS5 LS1 LS2 LS3 LS4 LS5 USC DSC; 
 MISSING=.;
 
ANALYSIS:

    TYPE =general RANDOM;  
    estimator=MLR; 
    ALGORITHM=INTEGRATION;
    convergence=0.01;
   
MODEL:
  
    ASC by ASC1-ASC5;
    OSC by OSC1-OSC6;
    SCO by ASC@0.8 OSC@0.8;
    
    USC@1;
    DSC@1;
    SCOU | SCO XWITH USC;
    SCOD | SCO XWITH DSC;
    
    RD by RD1-RD5;
    POS by POS1-POS5;
    
    LS by LS1-LS5;
    
    RD on SCO(a1)
          USC
          SCOU(w1);
    POS on SCO(a2)
           DSC
           SCOD(w2);
    LS on SCO(c)
          RD(b1)
          POS(b2);
   
MODEL CONSTRAINT:

   NEW(L_UDSC M_UDSC H_UDSC
      IE_L_RD IE_M_RD IE_H_RD
      IE_L_POS IE_M_POS IE_H_POS
      
      DIF_LM_RD DIF_LH_RD DIF_MH_RD
      DIF_LM_POS DIF_LH_POS DIF_MH_POS
      );
   
    L_UDSC = -1;
    M_UDSC = 0;
    H_UDSC = 1;
   
    IE_L_RD = (a1+w1*L_UDSC)*b1;
    IE_M_RD = (a1+w1*M_UDSC)*b1;
    IE_H_RD = (a1+w1*H_UDSC)*b1;
   
    IE_L_POS = (a2+w2*L_UDSC)*b2;
    IE_M_POS = (a2+w2*M_UDSC)*b2;
    IE_H_POS = (a2+w2*H_UDSC)*b2;
   
    DIF_LM_RD = IE_L_RD - IE_M_RD;
    DIF_LH_RD = IE_L_RD - IE_H_RD;
    DIF_MH_RD = IE_M_RD - IE_H_RD;
    
    DIF_LM_POS = IE_L_POS - IE_M_POS;
    DIF_LH_POS = IE_L_POS - IE_H_POS;
    DIF_MH_POS = IE_M_POS - IE_H_POS;
   
    PLOT(P_L_RD P_M_RD P_H_RD
         P_L_POS P_M_POS P_H_POS);
    LOOP(XVAL,1,7,0.1);
    P_L_RD = IE_L_RD*XVAL;
    P_M_RD = IE_M_RD*XVAL;
    P_H_RD = IE_H_RD*XVAL;
    
    P_L_POS = IE_L_POS*XVAL;
    P_M_POS = IE_M_POS*XVAL;
    P_H_POS = IE_H_POS*XVAL;
   
OUTPUT:

    cint;
    STDYX
PLOT:

    TYPE = plot2;
   
