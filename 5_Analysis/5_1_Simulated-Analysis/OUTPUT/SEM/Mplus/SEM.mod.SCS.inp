TITLE:

    Moderator is SCS(UC DC);
   
DATA:
FILE = "./OUTPUT/Mplus/SEM.mod.SCS_0e7cd27b59042453a896be71e366fc8f.dat";
 
VARIABLE:
NAMES = ASC1 ASC2 ASC3 ASC4 ASC5 OSC1 OSC2 OSC3 OSC4 OSC5 OSC6 RD1 RD2 RD3 RD4 RD5
     POS1 POS2 POS3 POS4 POS5 LS1 LS2 LS3 LS4 LS5 UC1 UC2 UC3 DC1 DC2 DC3; 
 MISSING=.;
 
ANALYSIS:

    TYPE =general RANDOM;  
    estimator=MLR; 
    ALGORITHM=INTEGRATION;
    convergence=0.01;
    process=16;
   
MODEL:
  
    ASC by ASC1-ASC5;
    OSC by OSC1-OSC6;
    SCO by ASC@0.8 OSC@0.8;
    
    UC by UC1-UC3;
    UC@1;
    DC by DC1-DC3;
    DC@1;
    
    SCOUC | SCO XWITH UC;
    SCODC | SCO XWITH DC;
    
    RD by RD1-RD5;
    POS by POS1-POS5;
    
    LS by LS1-LS5;
    
    RD on SCO(a1)
          UC
          SCOUC(w1);
    POS on SCO(a2)
           DC
           SCODC(w2);
    LS on SCO(c)
          RD(b1)
          POS(b2);
   
MODEL CONSTRAINT:

   NEW(L_SCS M_SCS H_SCS
       IE_L_RD IE_M_RD IE_H_RD
       IE_L_POS IE_M_POS IE_H_POS
      
       DIF_LM_RD DIF_LH_RD DIF_MH_RD
       DIF_LM_POS DIF_LH_POS DIF_MH_POS
      );
   
    L_SCS = -1;
    M_SCS = 0;
    H_SCS = 1;
   
    IE_L_RD = (a1+w1*L_SCS)*b1;
    IE_M_RD = (a1+w1*M_SCS)*b1;
    IE_H_RD = (a1+w1*H_SCS)*b1;
   
    IE_L_POS = (a2+w2*L_SCS)*b2;
    IE_M_POS = (a2+w2*M_SCS)*b2;
    IE_H_POS = (a2+w2*H_SCS)*b2;
   
    DIF_LM_RD = IE_L_RD - IE_M_RD;
    DIF_LH_RD = IE_L_RD - IE_H_RD;
    DIF_MH_RD = IE_M_RD - IE_H_RD;
    
    DIF_LM_POS = IE_L_POS - IE_M_POS;
    DIF_LH_POS = IE_L_POS - IE_H_POS;
    DIF_MH_POS = IE_M_POS - IE_H_POS;
   
    PLOT(P_L_RD P_M_RD P_H_RD
         P_L_POS P_M_POS P_H_POS);
    LOOP(XVAL,1,7,0.1);
    P_L_RD = IE_L_RD*XVAL;
    P_M_RD = IE_M_RD*XVAL;
    P_H_RD = IE_H_RD*XVAL;
    
    P_L_POS = IE_L_POS*XVAL;
    P_M_POS = IE_M_POS*XVAL;
    P_H_POS = IE_H_POS*XVAL;
   
OUTPUT:

    cint;
    STDYX
PLOT:

    TYPE = plot2;
   
